apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

Properties props = new Properties()
props.load(new FileInputStream(rootProject.file('signing.properties')))

android {

    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "APP_ID", props["APP_ID"]
        buildConfigField "String", "API_KEY", props["API_KEY"]
        buildConfigField "String", "TestAccount", props["TestAccount"]
        buildConfigField "String", "TestPassword", props["TestPassword"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        uat {
            applicationId "com.tonynowater.uniforminvoicehelper.uat"
            dimension "default"
            manifestPlaceholders = [gradle_app_name: "統一發票小幫手(UAT)"]
            resValue "string", "app_name", "統一發票小幫手(測試)"
        }

        prod {
            applicationId "com.tonynowater.uniforminvoicehelper.prod"
            dimension "default"
            manifestPlaceholders = [gradle_app_name: "統一發票小幫手"]
            resValue "string", "app_name", "統一發票小幫手"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation rootProject.ext.androidDeps.androidSupportLib
    implementation rootProject.ext.androidDeps.androidDesignLib
    implementation rootProject.ext.androidDeps.constraintLayout

    //retrofit
    implementation rootProject.ext.netDeps.retrofit2
    implementation rootProject.ext.netDeps.retrofitGson
    implementation rootProject.ext.netDeps.retrofitScalars
    implementation rootProject.ext.netDeps.retrofitRxAdapter
    implementation rootProject.ext.netDeps.okhttpLog

    //dagger2
    implementation rootProject.ext.dagger2Deps.dagger2
    kapt rootProject.ext.dagger2Deps.dagger2Compiler
    testAnnotationProcessor rootProject.ext.dagger2Deps.dagger2Compiler
    compileOnly rootProject.ext.dagger2Deps.javaxAnnotation

    //dagger.android
    implementation rootProject.ext.dagger2Deps.daggerAndroidSup
    kapt rootProject.ext.dagger2Deps.daggerAndroidCompilier

    //kotlin
    implementation rootProject.ext.kotlinDeps.kotlinStdLib
    implementation rootProject.ext.kotlinDeps.kotlinReflect
    testImplementation rootProject.ext.kotlinDeps.kotlinTestJunit

    //android arch component
    implementation rootProject.ext.archDeps.archRuntime
    implementation rootProject.ext.archDeps.archExtensions
    implementation rootProject.ext.archDeps.room
    kapt rootProject.ext.archDeps.archCompiler
    kapt rootProject.ext.archDeps.roomCompiler

    //rx
    implementation rootProject.ext.rxDeps.rxJava
    implementation rootProject.ext.rxDeps.rxAndroid

    //bar code
    implementation rootProject.ext.barCodeDeps.barCode

    //material dialog
    implementation rootProject.ext.meterialDesignDialogDeps.materialDialog

    //test
    testImplementation rootProject.ext.testDeps.junit
    testImplementation rootProject.ext.testDeps.mockito
    testImplementation rootProject.ext.testDeps.mockitoInline
    testImplementation rootProject.ext.testDeps.robolectric
    testImplementation rootProject.ext.testDeps.robolectricV4
    testImplementation rootProject.ext.testDeps.runner

    androidTestImplementation rootProject.ext.testDeps.runner
    androidTestImplementation rootProject.ext.testDeps.espresso

    implementation project(path: ':datelimitpickerdialog')
}
